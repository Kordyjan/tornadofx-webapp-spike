group 'pl.edu.agh.ozab'
version '0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.2-2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7"
    }
}

ext.port = '4567'

subprojects {
    ext.subProjectType = 'lib'
    afterEvaluate {
        if (it.subProjectType == 'back') {
            it.apply plugin: 'application'
            it.run.args parent.projectDir.toPath().resolve('runtime-data/data.json').toString(), port
        } else if (it.subProjectType == 'front') {
            it.apply plugin: 'application'
            it.run.args 'localhost', port
        }
    }
}

task clearState(type: Delete) {
    delete = 'runtime-data'
}

